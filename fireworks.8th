needs nk/gui
ns: game
"lib/sugar.8th" f:include
with: n
with: G
with: nk
with: game
"lib/entity.8th" f:include
"lib/repl.8th" f:include
"lib/game.8th" f:include
"lib/graphics_nk.8th" f:include

: ?die
    y@ 0 < 
    y@ 480 > or
    x@ 0 < or
    x@ 640 > or
        if me @ disable ;;; then ;

: box  0xffff0000 fore ! 6 6 rectf ;

: *particle  SED: -- m
	one {{
		frnd 8 fmod 4 - vx!
		frnd 7 fmod 5 - vy!
        ( box ) render!
        ( ?die  0.1 vy+! ) update!
        ( 
             1 after
             ( ?die ) update!
             0 vy! 4 vx!
             1 after
             4 vy! 0 vx!
        ) think!
    me @ }} ;
    
: *orb  SED: -- m
	one {{
		( $ffee2211 fore ! 32 circf ) render!
		(
			x@ y@ at  ( *particle drop ) 4 times

            repl @ not if
                KEY_ENTER key-pressed? if
                    x@ y@ at  ( *particle drop ) 100 times 
                then
    
                KEY_LEFT key-down? if
                    -3 x+!
                then
                KEY_RIGHT key-down? if
                    3 x+!
                then
                KEY_UP key-down? if
                    -3 y+!
                then
                KEY_DOWN key-down? if
                    3 y+!
                then
                
            then

        ) update!
	me @ }} ;

null gvar it
320 240 at *orb it !

: new-window  SED: --
    {
        name: "main",
        wide: 640 ,
        high: 480 ,
        bg: "palegreen",
        fonts: {
            font1: ` 24 font:system ` ,
            font2: ["tahoma:24", "arial:24", "ubuntu:24"]
        },
        font: "font1",
        title: "Fireworks"
    }
    nk:win ;

: warm  SED: --
    entity-root root !
	game
;

: startup  SED: --
    new-window
    warm
;

: app:main  SED: --
    startup
;

